Correction de l'examen 2018/2019
================================

[Sketch link](https://sketch.sh/s/dgfrHHkNzdUuf3VYTRO3Vy)

[Le sujet](examen1819.pdf)

Ce sujet a été écrit par Michele Pagani.

## Exercice 1

Qu'affiche l'interpréteur OCaml lorsque l'on évalue l'une après l'autre les lignes ci-dessous ? Pour chaque expression ou définition, indiquer : son type ; sa valeur (ou <fun> s'il s'agit d'une valeur de fonction) ; le cas échéant, son effet de bord ou l'exception levée. Si l'expression est mal typée ou incorrecte, indiquer le message d'erreur. Justifier si nécessaire.

```ocaml
let x = 2010 + let x = 4 in 4 + x;;

x;;

exception Invalid_argument of int;;

let f x = if x < 0 then raise (Invalid_argument x) else x ;;

(* let f x = if x < 0 then print_endline "Invalid argument" else x ;; 

  "x" est de type "int" mais devrait avoir le type "unit" dans le contexte de l'expression "false" du if-then-else.

*)

let f x =
  if x < 0 then
    print_endline "Invalid argument"
  else
    raise (Invalid_argument x) ;;

(* f (-1); f 1 ;; 

  produit une exception non rattrapée "Invalid_argument 1".

*)

try f (-1); f 1 with Invalid_argument x -> print_int x ;;

try f 1 ; f (-1)with Invalid_argument x -> print_int x ;;

(* Boucle:
let rec loop () =
  try 
     f (-1) ; f 1
  with Invalid_argument x -> loop ()
in loop () ;;
*)
```

## Exercice 2

Réécrire le programme ci-dessous en style fonctionnel pur, i.e. en n'utilisant ni boucles, ni références, ni tableaux, ni d'autres données modifiables. Attention ! Les deux fonctions doivent avoir exactement le même type et donner les mêmes sorties sur les mêmes entrées.

*Indication: Vous pouvez déclarer des fonctions auxiliaires si vous le souhaitez.*



```ocaml
let erat n =
  let a = Array.init (n - 1) (fun i -> i + 2) in
  let r = ref []  in
  for i = 0 to n - 2 do
    if a.(i) > 0 then
      let x = a.(i) in begin
        r := x :: !r ;
        for j = i + 1 to n - 2 do
          if a.(j) mod x = 0 then a.(j) <- 0
        done          
      end
  done;
  !r
  
let prime_to_100 = erat 100
```

Cette fonction calcule les entiers premiers inférieurs à `n` en utilisant la technique du Crible d'Eratosthène. Une solution fonctionnelle possible était :

```ocaml
let rec range start stop accu =
  if start > stop then accu else range start (stop - 1) (stop :: accu)

let not_divisible_by n = fun k -> k mod n > 0

let remove_multiples n xs = List.filter (not_divisible_by n) xs

let erat n =
  let rec loop r = function
    | [] -> r
    | h :: tl -> loop (h :: r) (remove_multiples h tl)
  in
  loop [] (range 2 n [])
  
let prime_to_100 = erat 100

(* ou encore, dans un style d'ordre supérieur : *)

let fold_rewrite f accu l =
  let rec aux accu = function [] -> accu | x :: xs -> let accu, xs' = f accu x xs in aux accu xs' in
  aux accu l
  
let erat n = fold_rewrite (fun accu x xs -> (x :: accu, remove_multiples x xs)) [] (range 2 n [])

let prime_to_100 = erat 100

```

## Exercice 3

Etant donnée une liste `l = [a1; ... ; an]`, les préfixes de `l` sont les listes de la forme `[a1; ... ; ai]` pour `i` pris dans `[0, ..., n]`.  Programmer les fonctions suivantes. L'utilisation de la récursion terminale sera appréciée.

### Question 1
`find_prefix : ('a list -> bool) -> 'a list -> 'a list * 'a list`. Etant données un prédicat `p` sur les listes et une liste `l`, `find_prefix p l` doit renvoyer le couple `(l1, l2)` où `l1` est le plus petit préfixe de `l` satisfaisant `p` et `l = l1 @ l2`. Si un tel préfixe n'existe pas, la fonction lancera l'exception `Not_found`.


```ocaml
let find_prefix p l =
  let rec aux l1 = function
  | [] -> raise Not_found
  | x :: xs -> let l1 = l1 @ [x] in if p l1 then (l1, xs) else aux l1 xs
  in
  if p [] then ([], l) else aux [] l
  
(** 
 
    Il ne fallait pas oublier les cas limites de cette fonction.
 
    Cette fonction est quadratique mais on ne peut pas vraiment faire mieux. 
    C'est une illustration que les listes standards ne sont pas la bonne structure de données
    pour manipuler l'ensemble des préfixes d'une séquence.
 
 *)
 
let sum l = List.fold_left ( + ) 0 l

let l1, l2 = find_prefix (fun l -> sum l > 5) [1;2;3;4;5]

let fail = try Some (find_prefix (fun l -> sum l > 500) [1;2;3;4;5]) with Not_found -> None

(* Une autre solution plus modulaire à ce problème consistait à se donner une fonction capable de
   produire les décompositions (préfixe, suffixes) à la demande. Bien entendu, on ne s'attendait
   pas à ce qu'un étudiant produise une telle solution mais je vous la donne pour votre culture. *)
type 'a decomposition = { prefix : 'a list; suffix : 'a list }

type 'a step =
| EndOfEnumeration
(* La fonction a terminé son énumération. *)
| Next of 'a decomposition * (unit -> 'a step)
(* La fonction a une nouvelle décomposition à proposer et une fonction pour obtenir la suivante. *)

let decompose : 'a list -> 'a step = fun l ->
  let rec aux prefix suffix =
    let decomposition = { prefix; suffix } in
    let next () = 
      match suffix with 
      | [] -> EndOfEnumeration 
      | x :: xs -> aux (prefix @ [x]) xs
    in
    Next (decomposition, next)
  in
  aux [] l
  
let step1 = decompose [1;2;3]
let step2 = decompose []

let decompositions l =
  let rec aux = function
  | EndOfEnumeration -> []
  | Next (d, n) -> d :: aux (n ())
  in
  aux (decompose l)
  
let decompositions1 = decompositions [1;2;3;4;5]

let find_prefix p l =
  let rec aux = function
  | EndOfEnumeration -> raise Not_found
  | Next (d, n) -> if p d.prefix then (d.prefix, d.suffix) else aux (n ())
  in
  aux (decompose l)
  
let l1, l2 = find_prefix (fun l -> sum l > 5) [1;2;3;4;5]

let fail = try Some (find_prefix (fun l -> sum l > 500) [1;2;3;4;5]) with Not_found -> None
(* Vous allez comprendre l'intérêt de cette implémentation alternative dans la question suivante. *)
```


### Question 2

`forall_prefix : ('a list -> bool) -> 'a list -> bool`. Etant données une prédicat `p` sur les listes et une liste `l` vérifie que tous les préfixes satisfont `p`.



```ocaml
let forall_prefix p l =
  let rec aux l1 = function
  | [] -> true
  | x :: xs -> let l1 = l1 @ [x] in p l1 && aux l1 xs
  in
  p [] && aux [] l
  
let test1 = forall_prefix (fun l -> (l = [] || List.hd l < 5)) [1;2;3;4]
let test2 = forall_prefix (fun l -> (l = [] || List.hd l >= 5)) [1;2;3;4]
let test3 = forall_prefix (fun l -> l <> []) []

(** Avec la solution alternative, on obtient: *)
let forall_prefix p l =
  let rec aux = function
  | EndOfEnumeration -> true
  | Next (d, n) -> p d.prefix && aux (n ())
  in
  aux (decompose l)
  
let test1 = forall_prefix (fun l -> (l = [] || List.hd l < 5)) [1;2;3;4]
let test2 = forall_prefix (fun l -> (l = [] || List.hd l >= 5)) [1;2;3;4]
let test3 = forall_prefix (fun l -> l <> []) []
(** Comme on a séparé la logique de calcul des décompositions du traitement de ces dernières, 
    le code est plus clair car on a mieux décomposé le problème. *)

```

### Question 3

`cartesian_map : ('a -> 'b -> 'c) -> 'a list -> 'b list -> 'c list` telle que 

``
cartesian_map f [a1; ...; an] [b1; ..; bk]
``

s'évalue en:

``       
f a1 b1; ...; f an b1; ....; f a1 bk;... ; f an bk]
``

ou toute autre liste contenant les mêmes éléments. 


```ocaml
let cartesian_map f l1 l2 =
  List.map (fun x -> List.map (fun y -> f x y) l2) l1 |> List.flatten
  
let test = cartesian_map (fun x y -> (x, y)) [1;2;3] [1;2;3;4]
```

## Exercice 4

Appelons *mot sur l'alphabet*  `{-1, 1}` toute suite `u = < u_1, ..., u_n > `telle que `u_i ∈ {-1, 1}` pour chaque `i ∈ [1, ..., n]`. La longueur `n` de ce mot se note `|u|`. 

On dira que `u` est un *mot de pile* s'il vérifie les deux propriétés suivantes:

1. la somme des éléments de `u` vaut `-1`.

2. la somme des éléments de tout préfixe strict de `u` est positive ou nulle.

On note par un point `.` la concaténation des mots. Par exemple, si `a = < a_1, ..., a_n >` et `b = < b_1, ..., b_m >`, on a `< a . b = a_1, ..., a_n, b_1, ..., b_m >`.

Les mots de pile seront représentés en OCaml par des listes d'entiers, i.e. à l'aide du type `int list`. Les fonctions de l'exercice 3 sont librement utilisables dans les questions, même si vous n'avez pas réussi à les écrire.

### Question 1

Donner toutes les valeurs en OCaml correspondant aux mots de pile de longueur `1` et `3` et `5`. Notez qu'un mot de pile est nécessairement de longueur impaire.

```ocaml
let stack1 = [[-1]]
let stack3 = [[1;-1;-1]]
let stack5 = [[1;-1;1;-1;-1]; [1;1;-1;-1;-1]]
```

### Question 2

Écrire une fonction `is_stack : int list -> bool` vérifiant si un mot est un mot de pile.

```ocaml
let is_stack l =
  sum l = -1 && forall_prefix (fun p -> p = l || sum p >= 0) l

(* Le test p = l sert à limiter le test aux cas où p est un préfixe strict de l. *)
```

On remarque que si `v` et `w` sont des mots de pile, alors `< 1 > . v . w` est aussi un mot de pile. Inversement, tout mot de pile de longueur supérieure ou égale à `3` se décompose de manière unique sous la forme `< 1 > . v . w`, où `v` et `w` sont des mots de pile et `v` est de longueur minimale.

### Question 3

Pour chacun des mots suivants, trouver les mots `v`, `w` tels que  `< 1 > . v . w` soit égal à ce mot :
- `< 1, 1, 1, -1, -1, -1, -1 >`
- `< 1, -1, 1, -1, 1, -1, -1 >`

```ocaml
let decomposition1 = ([1; 1; -1; -1; -1], [-1])
let decomposition2 = ([-1; 1; -1], [1; -1; -1])
```

### Question 4

Écrire une fonction `decompose_stackword : int list -> (int list * int list)` qui, étant donné un mot de pile `u` de longueur supérieure ou égale à `3`, renvoie le couple `(v,  w)` correspondant à sa décomposition. La fonction devra lancer une exception si le mot fourni en entrée n'est pas un mot de pile.

```ocaml
let decompose_stackword = function
| 1 :: ds -> 
  let (l1, l2) = find_prefix is_stack ds in
  if is_stack l2 then (l1, l2) else raise Not_found
| _ -> raise Not_found

let test1 = decompose_stackword [1;1;1;-1;-1;-1;-1]
let test2 = decompose_stackword [1;-1;1;-1;1;-1;-1]
```

On souhaite à présent calculer *tous* les mots de pile d'une longueur donnée.

- a. Écrire une fonction

  `add : int list list -> int list list -> int list list -> int list list`

  telle que `add l1 l2 l` ajoute en tête de la liste `l` tous les éléments de la forme `1 :: (t1 @ t2)`, où `t1` est un élément de `l1` et `t1` un élément de `l2`.



```ocaml
let add l1 l2 l =
  cartesian_map (fun t1 t2 -> 1 :: (t1 @ t2)) l1 l2 @ l
```

- b. En déduire une fonction `stackwords : int -> int list` qui, pour tout entier `n`, renvoie la liste des mots de pile de longueur inférieure ou égale à `2 * n + 1`.


```ocaml
let rec stackwords = function
| 0 -> [[-1]]
| 1 -> [[1;-1;-1]]
| n -> aux n 0 []
and aux n k l =
  if k = n then 
    l 
  else
    aux n (k + 1) (add (stackwords k) (stackwords (n - k - 1)) l)

let test5 = stackwords 2
let test7 = stackwords 3
```
